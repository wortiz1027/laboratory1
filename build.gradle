plugins {
	id 'java'
	id 'jacoco'
	id 'org.owasp.dependencycheck' version '8.2.1'
	id 'org.springframework.boot' version '3.0.4'
	id 'io.spring.dependency-management' version '1.1.0'
}

group = 'dev.laboratory'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

jacoco {
	toolVersion = "0.8.8"
	reportsDirectory = layout.buildDirectory.dir('reports')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok:1.18.26'
	annotationProcessor 'org.projectlombok:lombok:1.18.26'

	implementation 'org.mapstruct:mapstruct:1.5.3.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

	runtimeOnly 'com.mysql:mysql-connector-j'

	implementation 'org.flywaydb:flyway-core:8.4.4'
	implementation 'org.flywaydb:flyway-mysql:8.4.4'

	testImplementation "org.testcontainers:testcontainers:1.17.6"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
	jacoco {
		destinationFile = layout.buildDirectory.file('reports/jacoco/jacocoTest.exec').get().asFile
		classDumpDir = layout.buildDirectory.dir('reports/jacoco/classpathdumps').get().asFile
	}
}

tasks.named("bootJar") {
	archiveClassifier = 'boot'
	archiveFileName = "laboratory1-services.jar"
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('reports/jacoco/html')
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: 'dev/laboratory/laboratory1/Application**')
		}))
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 1.0
			}
			excludes = [
					'dev.laboratory.laboratory1.Application.*'
			]
		}
	}
}
